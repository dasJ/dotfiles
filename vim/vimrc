
" ###
" General options
" ###
set nocompatible " be iMproved
set autoread " Reload if changed on disk
set backspace=indent,eol,start " Make backspace work everywhere
set backupcopy=yes " Preserve Attributes of original file
set backupdir=~/.vim/backup " See previous option
set backupskip=/tmp/* " No Backup for /tmp
set completeopt=longest,menuone " Only insert the longest common text of the matches. Show a menu even if there is just one
set tabpagemax=9999
set directory=~/.vim/swap,~/tmp,. " Keep swap files in home
set path=.,/,,@HOME@/projects/nixpkgs/lib,@HOME@/projects/nixpkgs/pkgs/top-level
set viminfo+=n~/.vim/viminfo
set history=50 " : History
set hlsearch " Highlight search terms
set ignorecase " Case insensitive search
set incsearch " Search as you type
set smartcase " If search pattern contains a uppercase letter, case sensitivity is on
set laststatus=2 " Always display status bar
set number " Line numbers
set ruler " Display position in file in status bar
set scrolloff=5 " Keep cursor 5 lines away from the screen border
set shiftwidth=4 " 4 spaces = 1 tab
set showcmd " Show what you are doing
set showmatch " Show matching bracket
set showmode " Show whether in insert mode
set smarttab " Be cool when inserting tabs
set title " Change shell title
set wildmenu " Command line completion
set wildmode=list:longest,full " Show everything
set encoding=utf-8 " Encoding
set termencoding=utf-8 " Terminal Encoding
set winminheight=0 " Windows can have no height
set whichwrap=b,s,h,l,<,>,[,]   " Backspace and cursor keys wrap, too
set list " Show whitespaces
set listchars=tab:>-,eol:Â¶,trail:~ " Show these whitespaces
set virtualedit=onemore " Allow the cursor past the end of line
set splitright " Put a new window to the right
set splitbelow " Put a new window below
set autoindent " Indent as prevous line
set noexpandtab " Do not expand tabs to spaces
set tabstop=4 " Indentation every 4 columns
set softtabstop=4 " Let backspace delete indent
set linespace=0 " No space between the characters
set shiftwidth=4 " Indent with 4 spaces
set clipboard=unnamedplus " Default clipboard
set t_Co=256 " Wow! Much color!
set cursorline " Highlight current line
set runtimepath^=/run/current-system/sw/share/vim-plugins

" ###
" Plugins
" ###
"
filetype off
call plug#begin('~/.vim/plugged')
" Main plugs
Plug 'majutsushi/tagbar' " Can not be loaded on demand because airline needs it
Plug 'rkitover/vimpager', { 'on': 'NeverCallThis' }
Plug 'kopischke/vim-fetch' " Can not be loaded on demand
Plug 'w0rp/ale' " Usable for most languages
Plug 'takac/vim-hardtime' " Make my life harder
" Language-specific plugs
Plug 'fatih/vim-go', { 'for': 'go' }
Plug 'godlygeek/tabular', { 'for': 'markdown' }
Plug 'plasticboy/vim-markdown', { 'for': 'markdown' }
" Syntax highlightings
Plug 'PotatoesMaster/i3-vim-syntax'
Plug 'vim-scripts/openvpn'
Plug 'LnL7/vim-nix'
" Visual things
Plug 'tomasr/molokai' " Not loaded on demand because I always need it
Plug 'vim-airline/vim-airline' " See molokai
call plug#end()
filetype plugin indent on

" ###
" Variables
" ###
let maplocalleader=","
let mapleader=","
let g:molokai_original=1
let g:airline#extensions#tabline#enabled = 1
let g:netrw_mousemaps = 0 " No mouse
let g:netrw_banner = 0 " No banner
let g:netrw_liststyle = 3 " Tree

" ###
" Keymappings
" ###
map <LocalLeader>t <Esc>:tabnew<CR>
set pastetoggle=<F12> " Sane indentation on pastes with F12
cnoreabbrev <expr> W ((getcmdtype() is# ':' && getcmdline() is# 'W')?('w'):('W'))
cnoreabbrev <expr> Q ((getcmdtype() is# ':' && getcmdline() is# 'Q')?('q'):('Q'))
cnoreabbrev <expr> X ((getcmdtype() is# ':' && getcmdline() is# 'X')?('x'):('X'))
nmap <F8> :TagbarToggle<CR>
" Disable stuff
for prefix in ['i', 'n', 'v']
	for key in ['<Up>', '<Down>', '<Left>', '<Right>', '<Home>', '<End>', '<PageUp>', '<PageDown>', '<Del>', '<Ins>']
		exe prefix . "noremap" . key . " <Nop>"
	endfor
endfor
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" ###
" Folding
" ###
if has ('folding')
	set foldenable " Enable folding
	set foldmethod=indent " Indentation to specify folds
	set foldcolumn=1 " Show a column with fold info
	set foldlevelstart=99 " No folding by default
	" Use F9 for folding:
	inoremap <F9> <C-O>za
	nnoremap <F9> za
	onoremap <F9> <C-C>za
	vnoremap <F9> zf
endif

" ###
" Commands
" ###
" emptyhash
command Eh :normal osha256 = "0000000000000000000000000000000000000000000000000000";<ESC>2T"

" ###
" Filetypes
" ###
autocmd BufNewFile,BufReadPost *.md set ft=markdown
autocmd Filetype yaml set ts=2 sw=2 et
autocmd Filetype typescriptreact set ts=4 sw=4 et

" ###
" Linting
" ###
let g:ale_linters = {
\	'typescriptreact': ['tsserver', 'eslint']
\}
let g:ale_fixers = {
\	'typescriptreact': ['prettier']
\}

" ###
" Hex Mode (RO)
" ###

command -bar Hexmode call ToggleHex()

function ToggleHex()
	" hex mode should be considered a read-only operation
	" save values for modified and read-only for restoration later,
	" and clear the read-only flag for now
	let l:modified=&mod
	let l:oldreadonly=&readonly
	let &readonly=0
	let l:oldmodifiable=&modifiable
	let &modifiable=1
	if !exists("b:editHex") || !b:editHex
		" save old options
		let b:oldft=&ft
		let b:oldbin=&bin
		" set new options
		setlocal binary " make sure it overrides any textwidth, etc.
		silent :e " this will reload the file without trickeries
		"(DOS line endings will be shown entirely )
		let &ft="xxd"
		" set status
		let b:editHex=1
		" switch to hex editor
		%!xxd
	else
		" restore old options
		let &ft=b:oldft
		if !b:oldbin
			setlocal nobinary
		endif
		" set status
		let b:editHex=0
		" return to normal editing
		%!xxd -r
	endif
	" restore values for modified and read only state
	let &mod=l:modified
	let &readonly=l:oldreadonly
	let &modifiable=l:oldmodifiable
endfunction

" ###
" Visual things
" ###
syntax enable
highlight clear LinNr
try
	colorscheme molokai
catch /^Vim\%((\a\+)\)\=:E185/
endtry

if &term =~ '256color'
	" disable Background Color Erase (BCE) so that color schemes
	" render properly when inside 256-color tmux and GNU screen.
	" see also http://snk.tuxfamily.org/log/vim-256color-bce.html
	set t_ut=
endif

if exists("vimpager")
	set nofoldenable
	set nonumber
	set norelativenumber
	set nocursorline
	set nolist
	set mouse=
	let g:airline#extensions#tabline#enabled = 0
endif

source ~/.dotfiles/local/vim
