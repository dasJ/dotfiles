#compdef deploy

_arguments -C \
	"1:hostname:->hostname" \
	"*::arg:->args"

case "$state" in
	(hostname)
		_values -s , 'hostname' "${(uonzf)$(ls /var/src/helsinki/1systems)}"
		;;
	(args)
		_values 'action' \
			'switch[Build and switch now]' \
			'boot[Build and switch at next boot]' \
			'reboot[Build and reboot for next switch]' \
			'test[Build and roll back on next boot]' \
			'eval[Evaluate and output the result]' \
			'build[Build and output the result]' \
			'build-vm[Build and create VM]' \
			'build-vm-with-bootloader[Build and create VM with bootloader]' \
			'install[Build and install to /mnt]'

		_arguments -C \
			"--local-build[Build locally]" \
			"--no-upgrade[Don't upgrade nixpkgs channel on local deployment]" \
			"--no-multiplex[Do not multiplex SSH sessions]" \
			"--no-hydra[Do not attempt to substitute against Hydra]" \
			"--offline[Do not attempt to substitute from anywhere]" \
			"--connect-to[Override connectTo for this run]:hosts:_hosts" \
			"--connect-user[Override connection user for this run]" \
			"--jump[Jump over a host via SSH]:hosts:_hosts" \
			"--nixpkgs[Override path to nixpkgs for this run]:filename:_directories" \
			"--ask[Run dry-activate and ask whether to continue before performing action]" \
			"--closure[Take this store-path as a system, instead of evaluating a config and building it somewhere]:filename:_directories" \
			"--backup[Run heb.service before deployment]" \
			"--autoreboot[Reboot if the deployment is not done after N seconds]:SEC" \
			"--autorevert[Switch back if the deployment is not done after N seconds]:SEC" \
			"--confirm[Require user to confirm deployment for --autoreboot and --autorevert]"
		;;
esac
